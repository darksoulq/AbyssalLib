plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.17'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'com.github.darksoulq'
version = '1.5.0-mc1.21.7'

def targetJavaVersion = 21

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()

    maven {
        name = 'papermc-repo'
        url = uri('https://repo.papermc.io/repository/maven-public/')
    }

    maven {
        name = 'sonatype'
        url = uri('https://oss.sonatype.org/content/groups/public/')
    }

    maven {
        name = 'magmaguy-repo'
        url = uri('https://repo.magmaguy.com/releases')
    }
}

dependencies {
    paperweight.paperDevBundle('1.21.7-R0.1-SNAPSHOT')
    implementation('org.xerial:sqlite-jdbc:3.44.1.0')
    implementation('org.apache.pdfbox:fontbox:3.0.5')
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.0'

    compileOnly('com.magmaguy:ResourcePackManager:1.5.0')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author = true
    options.version = true
    options.links('https://docs.oracle.com/en/java/javase/21/docs/api/')

    exclude '**/internal/**'
    exclude '**/PacketInterceptor.java'
    exclude '**/InternalCommand.java'
    exclude '**/ConfigParser.java'
    exclude '**/util/**'
    exclude '**/BlockManager.java'
    exclude '**/MapLoader.java'
    exclude '**/AbyssalLib.java'
    exclude '**/Bootstrap.java'
}


tasks.runServer {
    minecraftVersion '1.21.5'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
