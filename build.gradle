plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.17'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'com.github.darksoulq'
version = '1.2.7-mc1.21.5'

def targetJavaVersion = 21

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()

    maven {
        name = 'papermc-repo'
        url = uri('https://repo.papermc.io/repository/maven-public/')
    }

    maven {
        name = 'sonatype'
        url = uri('https://oss.sonatype.org/content/groups/public/')
    }

    maven {
        name = 'magmaguy-repo'
        url = uri('https://repo.magmaguy.com/releases')
    }
}

dependencies {
    paperweight.paperDevBundle('1.21.5-R0.1-SNAPSHOT')
    implementation 'org.xerial:sqlite-jdbc:3.44.1.0'
    compileOnly 'com.magmaguy:ResourcePackManager:1.2.2'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

tasks.runServer {
    minecraftVersion '1.21.5'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
